//
// ****************************************************************************
// * Copyright (C) 2018, International Business Machines Corporation          *
// * All rights reserved.                                                     *
// ****************************************************************************
//

namespace com.ibm.streamsx.elasticsearch.test;
use com.ibm.streamsx.elasticsearch::ElasticsearchIndex;

/**
 * In this sample SPL application the ElasticsearchIndex operator requires the connection settings given in application configuration.
 * 
 * For example, when connecting with SSL connection to the Compose for Elasticsearch database in the IBM cloud, an application configuration needs to be created with the properties and userName, password, nodeList, sslEnabled=true.
 * You can extract these parameters from the connection string of the Compose For Elasticsearch IBM Cloud service.
 * 
 * If application configuration value is not present, then the parameter default values are used, like localhost:9200 (host:port) and sslEnabled=false;
 * 
 * @param indexName
 * Optional parameter change the index name, default: index1
 *
  */
public composite TestConsistentRegionMain
{
	param
		expression<rstring> $appConfigName: getSubmissionTimeValue("appConfigName", "es");
		expression<rstring> $indexName: getSubmissionTimeValue("indexName", "index1");
	graph

		stream<rstring result> CRTest = TestConsistentRegionAppConfig() {
			param
				appConfigName: $appConfigName;
				indexName: $indexName;
		}
}


public composite TestConsistentRegionAppConfig (output DummyResult)
{
	param
		expression<rstring> $appConfigName: "es";
		expression<rstring> $indexName: "test-index-cr";
		expression<float64> $drainPeriod: 5.0;
		expression<int32>   $numTuples: 200;
		
	graph

		() as JCP = JobControlPlane() {}

		@consistent(trigger=periodic, period=$drainPeriod)
		stream<rstring key, float32 counter> Documents = Beacon()
		{
			logic state : { mutable float32 i=0f; }
			param
				iterations : $numTuples;
				period : 0.1;
			output Documents:
			    key = "docid_" + (rstring)IterationCount(),
				counter = i++;
		}

		() as ElasticsearchSink = ElasticsearchIndex(Documents)
		{
			param
				appConfigName: $appConfigName;
				indexName: $indexName;
				idNameAttribute: key;
				//sslTrustStore: getThisToolkitDir()+"/etc/cacerts";
				//sslTrustStorePassword: "123456";
		}

		stream<rstring result> DummyResult = Custom() {
			logic
			onProcess: {				
				while (true) {
					submit ({result=""}, DummyResult);
					block(1.0);
				}
			}
		}

}

